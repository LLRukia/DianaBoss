package ShadowCounterSpell

import Warlock
import SkillHelper
import TriggerHelper
import EditorHelper
import GlobalDefines

public class ShadowCounterSpell extends WarlockSkill
    static let self_id = 'wl0f'
    static let mana_recovery = 90
    static let range = 700.
    static let cool_down = 35.
    static let mana_cost = 35
    static let desc = Color_Define.COLOR_NORMAL + "→反制Boss的技能（粉色字体）|n" +
        Color_Define.COLOR_WARLOCK + "↑反制成功后，为你恢复{0}点魔法值。|n".format(mana_recovery.toString()) +
        Color_Define.COLOR_CAST_RANGE + "→施法距离：{0}|n".format(range.toString(1)) +
        Color_Define.COLOR_COOL_DOWN + "→CD:{0}s".format(cool_down.toString(1))

    construct(unit u)
        super(u)

    override function listen()
        trigger_..registerUnitEvent(owner_, EVENT_UNIT_SPELL_EFFECT)
            ..addCondition(Filter(()->GetSpellAbilityId()==ShadowCounterSpell.self_id))
            ..addAction(function ShadowCounterSpell.cast)
            ..setData(this castTo int)

    static function cast()
        let owner = (GetTriggeringTrigger().getData() castTo ShadowCounterSpell).owner_
        let target = GetSpellTargetUnit()
        if SkillHelper.counter_spell(owner, target, "purpleblink.mdx")
            owner.setMana(owner.getMana() + mana_recovery)

@compiletime
function create_shadow_counter_spell()
    let editor = EditorHelper.create_channel_template(ShadowCounterSpell.self_id, emSkillKey.F)
    editor..setCastRange(1, ShadowCounterSpell.range)
        ..setManaCost(1, ShadowCounterSpell.mana_cost)
        ..setCooldown(1, ShadowCounterSpell.cool_down)
        ..setButtonPositionNormalX(2)
        ..setButtonPositionNormalY(1)
        ..setHotkeyNormal("F")
        ..setTargetType(1, emTargetType.Unit castTo int)
        ..setTargetsAllowed(1, TYPE_ALLOWED_ENEMY+",invulnerable,vulnerable")
        ..setName("ShadowCounterSpell")
        ..setTooltipNormal(1, Color_Define.COLOR_ICEMAGE + "Counter Spell" + Color_Define.COLOR_HOT_KEY + "(F)")
        ..setTooltipNormalExtended(1, ShadowCounterSpell.desc)
        ..setIconNormal("replaceabletextures\\commandbuttons\\counterspell.blp")

    destroy editor
