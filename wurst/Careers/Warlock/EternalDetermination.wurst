package EternalDetermination

import Warlock
import AbilityObjEditing
import ObjectIds
import BuffObjEditing
import TriggerHelper
import SkillHelper
import TimerUtils
import PlayerUnit
import Assert

public class EternalDetermination extends WarlockSkill
    static let self_id = 'wl0d'
    static let last_time = 1.5
    static let buff_id = 'wldd'
    static let self_name = "EternalDetermination"
    static let mana_cost = 5
    static let cool_down = 12.
    static let desc = Color_Define.COLOR_NORMAL + "↑术士永不灭！|n" +
        Color_Define.COLOR_LAST_TIME + "→持续{0}秒。|n".format(last_time.toString(1)) +
        Color_Define.COLOR_WARNING + "★使用此技能会消除掉大部分魔法效果。|n" +
        Color_Define.COLOR_COOL_DOWN + "→CD:{0}s"

    private timer last_timer_
    private Invulnerability buff_ = null
    construct(unit u)
        super(u)

    override function listen()
        trigger_..registerUnitEvent(owner_, EVENT_UNIT_SPELL_EFFECT)
            ..addCondition(Filter(()->GetSpellAbilityId()==EternalDetermination.self_id))
            ..addAction(function EternalDetermination.cast)
            ..setData(this castTo int)

    override function on_buff_removed(unit u)
        if not assert(u == owner_, "D buff error, unit not matched")
            return

        if buff_ != null
            destroy buff_
            buff_ = null
            (PlayerUnitHelper.get_instance(owner_) castTo PlayerUnit).set_invulnerable(false)

    static function cast()
        var self = GetTriggeringTrigger().getData() castTo EternalDetermination
        self.buff_ = SkillHelper.protect(self.owner_)
        self.owner_.remove_buff(BuffClearType.additions, BuffType.buffs)
        self.owner_.remove_buff(BuffClearType.additions, BuffType.debuffs)
        self.owner_.add_buff(EternalDetermination.buff_id, BuffClearType.stable, BuffType.buffs, self)
        (PlayerUnitHelper.get_instance(self.owner_) castTo PlayerUnit).set_invulnerable(true)
        self.last_timer_ = getTimer()
        self.last_timer_..setData(self castTo int)
            ..start(EternalDetermination.last_time, function on_buff_expired)

    static function on_buff_expired()
        let self = GetExpiredTimer().getData() castTo EternalDetermination
        self.clear()
    
    override function clear()
        if last_timer_ != null
            last_timer_.release()
            last_timer_ = null
        owner_.remove_buff(EternalDetermination.buff_id)

@compiletime
function create_divine_aegis()
    let ed = new AbilityDefinitionBeserk(EternalDetermination.self_id)
    ed..setLevels(1)
        ..setHotkeyNormal("D")
        ..setDamageTakenIncrease(1, 0)
        ..setMovementSpeedIncrease(1, 0)
        ..setAttackSpeedIncrease(1, 0)
        ..setDurationHero(1, EternalDetermination.last_time)
        ..setDurationNormal(1, EternalDetermination.last_time)
        ..setManaCost(1, 5)
        ..setCooldown(1, 12)
        ..setButtonPositionNormalX(1)
        ..setButtonPositionNormalY(1)
        ..setName(EternalDetermination.self_name)
        ..setTooltipNormal(1, Color_Define.COLOR_PRIEST + "Eternal Determination" + Color_Define.COLOR_HOT_KEY + "(D)")
        ..setTooltipNormalExtended(1, EternalDetermination.desc)
        ..setIconNormal("replaceabletextures\\commandbuttons\\wld.blp")
        ..setBuffs(1, EternalDetermination.buff_id.toRawCode())
    destroy ed

@compiletime
function create_ice_shield_buff()
    let ed = new BuffDefinition(EternalDetermination.buff_id, 'Bbsk')
    ed..setArtTarget(1, "wld.mdx")
    ..setTargetAttachments(1, 2)
    ..setTargetAttachmentPoint0(1, "origin")
    ..setTooltipNormal(1, Color_Define.COLOR_PRIEST + "Eternal Determination")
    ..setTooltipNormalExtended(1, Color_Define.COLOR_NORMAL + "此单位免疫伤害。")
    ..setIconNormal(1, "replaceabletextures\\commandbuttons\\wld.blp")
    destroy ed