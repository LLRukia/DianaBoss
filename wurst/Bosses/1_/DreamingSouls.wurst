package DreamingSouls

import IBoss
import ClosureTimers
import PlayerHelper
import SoundUtils

public class DreamingSouls extends IBossSkill
    static let break_time = 5.5
    static let sd = new SoundDefinition(Sounds.slowTarget1, false, true)
    private bool m_bcd = true
    private CallbackPeriodic m_detect = null
    private CallbackPeriodic m_cast = null
    private int m_count = 0
    private var call_back = new DreamingSouls_Cb(this)
    construct(IBoss owner)
        super(owner)

    ondestroy
        destroy call_back

    static class DreamingSouls_Cb implements Counter_Cb
        DreamingSouls self
        construct(DreamingSouls obj)
            self = obj
        override function cb()
            self.m_owner.m_self_unit..unpause().setInvulnerable(false)
            destroy self.m_cast
            self.m_owner.m_bpause_mana = false
            self.endcast()
    
    override function cast()
        unit uc = m_owner.m_self_unit
        m_owner.m_bpause_mana = true
        BossHelper.create_skill_text(uc.getPos(), "- 梦绕魂魄 -", SkillTextColor.counter)
        m_bcd = false
        doAfter(25) ->
            m_bcd = true
        
        uc..setInvulnerable(true)..pause()
        m_owner.waiting_counter(call_back)
        m_count = 0
        m_cast = doPeriodically(0.75 - CUR_MODE*0.02) cb->
            uc.setAnimation("spell")
            sd.playOnPoint(uc.getPos3Real())
            for i = 0 to PLAYER_NUM - 1
                unit u = players[i].get_unit()
                if u.isAlive()
                    u.addEffect("Abilities\\Spells\\Undead\\DeathPact\\DeathPactTarget.mdl", "chest")
                    m_owner.m_self_unit.damageTarget(u, 400. + CUR_MODE * (50))
            m_count++
            if m_count >= 4
                m_owner.m_self_unit..unpause().setInvulnerable(false)
                BossHelper.register_counter(false)
                m_owner.m_bpause_mana = false
                endcast()
                destroy cb
        

    override function detect()
        m_detect = doPeriodically(1) cb->
            priority += 1.3

    override function stop_detect()
        if m_detect != null
            destroy m_detect
            m_detect = null

    override function stop_cast()

    override function get_break() returns real
        return break_time

    override function judge() returns bool
        return m_owner.m_self_unit.getMana() < 700 and m_bcd